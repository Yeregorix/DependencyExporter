plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.16.0'
    id 'com.diffplug.spotless' version '6.13.0'
}

group 'net.smoofyuniverse'
version '1.0.6'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

gradlePlugin {
    plugins {
        DependencyExporter {
            id = 'net.smoofyuniverse.dependency-exporter'
            displayName = 'DependencyExporter'
            description = 'Export Gradle dependencies to JSON files.'
            implementationClass = 'net.smoofyuniverse.exporter.DependencyExporterPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/Yeregorix/DependencyExporter'
    vcsUrl = 'https://github.com/Yeregorix/DependencyExporter.git'
    tags = ['dependency', 'export', 'json']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
}

spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'dependency-exporter'
            from components.java

            pom {
                name = 'DependencyExporter'
                packaging = 'jar'
                description = 'Export Gradle dependencies to JSON file.'
                url = 'https://github.com/Yeregorix/DependencyExporter'

                scm {
                    connection = 'scm:git:https://github.com/Yeregorix/DependencyExporter.git'
                    developerConnection = 'scm:git:https://github.com/Yeregorix/DependencyExporter.git'
                    url = 'https://github.com/Yeregorix/DependencyExporter'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'Yeregorix'
                        name = 'Hugo Dupanloup'
                        email = 'yeregorix@smoofyuniverse.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ?
                    'https://oss.sonatype.org/content/repositories/snapshots/'
                    : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

            credentials {
                username findProperty('ossrhUsername')
                password findProperty('ossrhPassword')
            }
        }
    }
}

if (project.hasProperty('signing.keyId')) {
    apply plugin: 'signing'
    signing {
        sign publishing.publications.mavenJava
    }
}
